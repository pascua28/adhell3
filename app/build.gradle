plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    file('app.properties').withReader {reader ->
        Properties props = new Properties()
        props.load(reader)
        project.ext.appProps = props
    }

    file('build.properties').withReader {reader ->
        Properties props = new Properties()
        props.load(reader)
        project.ext.buildProps = props
    }

    def signReleaseApk = appProps['release.store.file'] ? true : false
    def signDebugApk = appProps['debug.store.file'] ? true : false

    defaultConfig {
        def buildNumber = buildProps['build.number'] ?: ""
        def buildDate = buildProps['build.date'] ?: ""
        def packageName = appProps['package.name'] ?: ""
        def domainLimit = appProps['domain.limit'] ?: "15000"
        def blockedDomainDurationUi = appProps['blockedDomain.duration.ui'] ?: "24"
        def blockedDomainDurationDb = appProps['blockedDomain.duration.db'] ?: "72"
        def domainPrefix = appProps['domain.prefix'] ?: "false"
        def sklKey = appProps['skl.key'] ?: ""
        def disableApps = appProps['enable.disableApps'] ?: "false"
        def appComponent = appProps['enable.appComponent'] ?: "false"
        def showSystemAppComponent = appProps['appComponent.showSystemApps'] ?: "false"
        def defaultHost = appProps['default.host'] ?: "https://gitlab.com/fusionjack/adhell3-hosts/-/raw/master/hosts"

        applicationId packageName
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "3.2." + buildNumber

        buildConfigField("String", "BUILD_DATE", '"' + buildDate + '"')
        buildConfigField("int", "DOMAIN_LIMIT", domainLimit)
        buildConfigField("int", "BLOCKED_DOMAIN_DURATION_UI", blockedDomainDurationUi)
        buildConfigField("int", "BLOCKED_DOMAIN_DURATION_DB", blockedDomainDurationDb)
        buildConfigField("boolean", "DOMAIN_PREFIX", domainPrefix)
        buildConfigField("String", "SKL_KEY", '"' + sklKey + '"')
        buildConfigField("boolean", "DISABLE_APPS", disableApps)
        buildConfigField("boolean", "APP_COMPONENT", appComponent)
        buildConfigField("boolean", "SHOW_SYSTEM_APP_COMPONENT", showSystemAppComponent)
        buildConfigField("String", "DEFAULT_HOST", '"' + defaultHost + '"')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    signingConfigs {
        release {
            if (signReleaseApk) {
                storeFile file(appProps['release.store.file'])
                storePassword appProps['release.store.password']
                keyAlias appProps['release.key.alias']
                keyPassword appProps['release.key.password']
            }
        }
        debug {
            if (signDebugApk) {
                storeFile file(appProps['debug.store.file'])
            }
        }
    }

    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            if (signDebugApk) {
                signingConfig signingConfigs.debug
            }
        }
        release {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
            zipAlignEnabled true
            if (signReleaseApk) {
                signingConfig signingConfigs.release
            }
        }
    }
}

task buildNumber {
    doLast {
        def buildNumber = 'git rev-list --count HEAD'.execute().text.trim().toInteger() + 1
        file('build.properties').withWriter { writer ->
            writer.write("build.number=${buildNumber}")
            writer.write("\n")
            writer.write("build.date=${new Date().format('yyyyMMdd')}")
        }
        ['git', 'add', 'app/build.properties'].execute()
        ['git', 'commit', "-m Build ${buildNumber}"].execute()
    }
}

dependencies {
    compileOnly files('libs/knoxsdk.jar')

    implementation files('libs/floatingactionmenu-0.8.9.aar')
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.nineoldandroids:library:2.4.0'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation "androidx.preference:preference:1.2.0"
    implementation 'androidx.fragment:fragment:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'

    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.11'

    implementation 'androidx.room:room-runtime:2.4.1'
    annotationProcessor 'androidx.room:room-compiler:2.4.1'

    implementation 'com.google.dagger:dagger:2.33'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.33'

    implementation 'com.google.guava:guava:30.1-android'

    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    testImplementation 'junit:junit:4.13.2'
}