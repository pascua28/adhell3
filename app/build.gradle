apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        def buildPropsFile = file('build.properties')
        def buildNumber

        if (buildPropsFile.canRead()) {
            Properties buildProps = new Properties()
            buildProps.load(new FileInputStream(buildPropsFile))
            buildNumber = buildProps['build.number'].toInteger()
        } else {
            throw new FileNotFoundException("Could not read build.properties!")
        }

        def appPropsFile = file('app.properties')
        def packageName = ""
        def domainLimit = "15000"
        def domainPrefix = "false"
        def sklKey = ""
        def disableApps = "false"
        def appComponent = "false"
        def showSystemAppComponent = "false"
        def defaultHost = "https://gitlab.com/fusionjack/hosts/raw/master/hosts"

        if (appPropsFile.exists()) {
            Properties appProps = new Properties()
            appProps.load(new FileInputStream(appPropsFile))
            if (appProps['package.name'] != null) {
                packageName = appProps['package.name']
            }
            if (appProps['domain.limit'] != null) {
                domainLimit = appProps['domain.limit']
            }
            if (appProps['skl.key'] != null) {
                sklKey = appProps['skl.key']
            }
            if (appProps['domain.prefix'] != null) {
                domainPrefix = appProps['domain.prefix']
            }
            if (appProps['enable.disableApps'] != null) {
                disableApps = appProps['enable.disableApps']
            }
            if (appProps['enable.appComponent'] != null) {
                appComponent = appProps['enable.appComponent']
            }
            if (appProps['appComponent.showSystemApps'] != null) {
                showSystemAppComponent = appProps['appComponent.showSystemApps']
            }
            if (appProps['default.host'] != null) {
                defaultHost = appProps['default.host']
            }
        }

        applicationId packageName
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "3.2." + buildNumber

        buildConfigField("int", "DOMAIN_LIMIT", domainLimit)
        buildConfigField("boolean", "DOMAIN_PREFIX", domainPrefix)
        buildConfigField("String", "SKL_KEY", '"' + sklKey + '"')
        buildConfigField("boolean", "DISABLE_APPS", disableApps)
        buildConfigField("boolean", "APP_COMPONENT", appComponent)
        buildConfigField("boolean", "SHOW_SYSTEM_APP_COMPONENT", showSystemAppComponent)
        buildConfigField("String", "DEFAULT_HOST", '"' + defaultHost + '"')
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'

        exclude 'META-INF/androidx.*'
        exclude 'META-INF/com.google.android.material_material.*'
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    buildTypes {
        debug {
            shrinkResources = true
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources = true
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    def appcompat_version = "1.3.0-alpha01"
    def preference_version = "1.1.1"
    def fragment_version = "1.3.0-alpha06"
    def lifecycle_version = "2.3.0-alpha04"
    def biometric_version = "1.0.1"
    def work_version = "2.4.0-beta01"
    def viewpager2_version = "1.1.0-alpha01"
    def swiperefreshlayout_version = "1.1.0-rc01"
    def room_version = "2.3.0-alpha01"
    def material_version = "1.3.0-alpha01"
    def dagger_version = "2.28"
    def rxandroid_version = "3.0.0"
    def rxjava_version = "3.0.4"
    def speeddial_version = "3.1.1"

    compileOnly files("libs/knoxsdk.jar")

    implementation "androidx.appcompat:appcompat:$appcompat_version"

    implementation "androidx.preference:preference:$preference_version"

    implementation "androidx.fragment:fragment:$fragment_version"

    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"

    implementation "androidx.biometric:biometric:$biometric_version"

    implementation "androidx.work:work-runtime:$work_version"

    implementation "androidx.viewpager2:viewpager2:$viewpager2_version"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefreshlayout_version"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation "com.google.android.material:material:$material_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "io.reactivex.rxjava3:rxandroid:$rxandroid_version"

    implementation "io.reactivex.rxjava3:rxjava:$rxjava_version"

    implementation "com.leinardi.android:speed-dial:$speeddial_version"
}